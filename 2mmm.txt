model new
model restore 'mmm1.4'


define read_from_file_txt(name)
    local f, allData, header, data_rows,data_line
    local i, j, row, row_data, ser_val,column_num,row_num
    local maps  ; 所有字段名到 map 的映射

    ; ====== 读取文件并处理内容 ======
    f = file.open(name, 'r') ;f代表文件
    allData = file.read(f) ;所有的数据
    file.close(f) ;

    header = string.split(string.simplify(allData(1)), ',')  ; 表头（列名） 
    data_rows = allData(2, list.size(allData));所有有效的数据，比如去除列名;所以参数2可以提出来作为方法参数startLine
    maps = map  ; 用于保存每个字段名对应的 map
    
    column_num = list.size(header);列数

    ; 初始化每个字段的空 map
    loop j(1, column_num) ;j代表的是列名索引值
        if header(j) != ''  ; 跳过空列名，确定每列的名称
            maps(header(j)) = map ;针对每一列定义一个map
        endif
    end_loop

    ; ====== 按行进行读取并填充每个字段对应的 map ======
    row_num = list.size(data_rows);行数
    loop i(1, row_num)
        row = string.simplify(data_rows(i)) ;获取每一行的数据,去除一些空格
       
        row_data = string.split(row, ',')  ;对每一行数据进行分割.可以理解数为组
        ser_key = row_data(1)              ;获取第一列的数据 当作key

        ; 将当前行的每列值加入对应 map 中
        loop j(1, column_num)
            if header(j) != ''
                maps(header(j))(ser_key) = row_data(j);核心 二维键值对
            endif
        end_loop
    end_loop
    
    return maps
end

define parse_data(filename,model_type)
    ; 读取 zone_property2.txt 文件
    local data = read_from_file_txt(filename);data返回的是二维键值对
    local group_map = data("group") ;取出一列的值，随便那一列其实都可以，因为key都是一样的

    local keys = list
    loop foreach local key map.keys(group_map)

        keys = list.append(keys, key);二维索引中的x值
    endloop
    local count = list.size(keys);所有key的数量

  local current_roof, rock, dens, young, poisson, fric, cohesion, ten
    ; 遍历数据
    loop local i(1,count)
        local curr_key = keys(i) ;去除当前的key值 实际上就是x值 即本例中的group
        
        ;接下来 根据二位索引获取值  (列行) 来进行定位一个数
        thick    = float(data('thick')(curr_key))
        floor    = float(data('floor')(curr_key))
        roof     = float(floor) + float(thick)
        dens     = float(data('dens')(curr_key))
        young    = float(data('young')(curr_key))
        poisson  = float(data('poisson')(curr_key))
        ;以下参数为mohr-coulomb使用
        cohesion = float(data('cohesion')(curr_key))
        fric     = float(data('fric')(curr_key))
        ten      = float(data('ten')(curr_key))
              
        if string.lower(model_type) = "mohr-coulomb"
           
            command
                block zone property dens @dens young @young poisson @poisson ...
                fric @fric cohesion @cohesion ten @ten range position-z @floor @roof
            endcommand
        else
            command
                block zone property dens @dens young @young poisson @poisson ...
                range position-z @floor @roof
            endcommand
        endif

    endloop
end



block zone cmodel assign elastic


[parse_data("zone_property2.txt","elastic")]


block contact jmodel assign mohr 
block contact property stiffness-normal 1e11 stiffness-shear 1e11 cohesion 2e7 friction 30 tension 1e7
block contact material-table default property stiffness-normal 1e11 stiffness-shear 1e11 cohesion 2e7 friction 30 tension 1e7
model save 'mmm2.0'
model grav 0 0 -10
block insitu topo ratio-x 0.5 ratio-y 0.5

; 边界条件和重力
block gridpoint apply velocity-x 0 range position-x  500633,500635
block gridpoint apply velocity-x 0 range position-x  499689,499690
block gridpoint apply velocity-y 0 range position-y  3947079,3947080
block gridpoint apply velocity-y 0 range position-y  3945646,3945647
block gridpoint apply velocity-z 0 range position-z  212,213

model solve ratio 1e-4
;model save 'mmm2.1'

block zone cmodel assign mohr-coulomb


[parse_data("zone_property2.txt","mohr-coulomb")]

block contact jmodel assign mohr 

block contact property stiffness-normal 1e11 stiffness-shear 1e11 cohesion 1e6 friction 30 tension 1e5  ; 统一接触属性
block contact property stiffness-normal 1e9 stiffness-shear 1e9 cohesion 1e3 friction 0 tension 1e2 range position-x 500073 500249 position-y 3945828 3946865 position-z 267.6 300.6
block contact property stiffness-normal 1e10 stiffness-shear 1e10 cohesion 1e4 friction 10 tension 1e3 range position-z 300.6 746
block contact material-table default property stiffness-normal 1e9 stiffness-shear 1e9 cohesion 0 friction 0 tension 0

fish define get_ini_stress
      loop foreach local zp block.zone.list
          block.zone.extra(zp,1) =block.zone.stress.zz(zp)
          block.zone.extra(zp,2) =block.zone.stress.min(zp)
      end_loop 
end
[get_ini_stress]

model save 'mmm2.2'

;call '3mmm.txt'
